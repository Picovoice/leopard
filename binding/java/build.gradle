plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

ext {
    PUBLISH_GROUP_ID = 'ai.picovoice'
    PUBLISH_VERSION = '2.0.4'
    PUBLISH_ARTIFACT_ID = 'leopard-java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

def outputDir = "$buildDir/classes/java/main"

task copyDefaultModel(type: Copy) {
    from('../../lib/common/leopard_params.pv') into "${outputDir}/leopard/lib/common/"
}
task copyLinuxLib(type: Copy) {
    from('../../lib/java/linux/x86_64/libpv_leopard_jni.so') into "${outputDir}/leopard/lib/java/linux/x86_64/"
}
task copyMacLib(type: Copy) {
    from('../../lib/java/mac/')
    include('x86_64/libpv_leopard_jni.dylib',
            'arm64/libpv_leopard_jni.dylib')
    into "${outputDir}/leopard/lib/java/mac/"
}
task copyWindowsLib(type: Copy) {
    from('../../lib/java/windows/')
    include('amd64/libpv_leopard_jni.dll',
            'arm64/libpv_leopard_jni.dll')
    into "${outputDir}/leopard/lib/java/windows/"
}
task copyRPiLib(type: Copy) {
    from('../../lib/java/raspberry-pi/')
    include('cortex-a53/libpv_leopard_jni.so',
            'cortex-a53-aarch64/libpv_leopard_jni.so',
            'cortex-a72/libpv_leopard_jni.so',
            'cortex-a72-aarch64/libpv_leopard_jni.so',
            'cortex-a76/libpv_leopard_jni.so',
            'cortex-a76-aarch64/libpv_leopard_jni.so')
    into("${outputDir}/leopard/lib/java/raspberry-pi/")
}

task copyTasks(type: GradleBuild) {
    tasks = ['copyDefaultModel',
             'copyLinuxLib',
             'copyMacLib',
             'copyWindowsLib',
             'copyRPiLib']
}

jar {
    dependsOn 'copyTasks'
}

javadoc {
    dependsOn 'copyTasks'
}

if (file("${rootDir}/publish-mavencentral.gradle").exists()) {
    apply from: "${rootDir}/publish-mavencentral.gradle"
}

dependencies {
    testImplementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.apache.commons:commons-text:1.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
}

compileTestJava {
    dependsOn 'assemble'
}

test {
    systemProperty 'pvTestingAccessKey', System.getProperty('pvTestingAccessKey')
    systemProperty 'numTestIterations', System.getProperty('numTestIterations')
    systemProperty 'initPerformanceThresholdSec', System.getProperty('initPerformanceThresholdSec')
    systemProperty 'procPerformanceThresholdSec', System.getProperty('procPerformanceThresholdSec')
    useJUnitPlatform()
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
