apply plugin: 'com.android.application'

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }
    if (project.hasProperty("numTestIterations")) {
        properties.put("numTestIterations", project.getProperty("numTestIterations"))
    }
    if (project.hasProperty("initPerformanceThresholdSec")) {
        properties.put("initPerformanceThresholdSec", project.getProperty("initPerformanceThresholdSec"))
    }
    if (project.hasProperty("procPerformanceThresholdSec")) {
        properties.put("procPerformanceThresholdSec", project.getProperty("procPerformanceThresholdSec"))
    }
}

android {
    compileSdkVersion defaultTargetSdkVersion

    defaultConfig {
        applicationId "ai.picovoice.leoparddemo"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
        resValue 'string', 'numTestIterations', properties.getProperty("numTestIterations", "")
        resValue 'string', 'initPerformanceThresholdSec', properties.getProperty("initPerformanceThresholdSec", "")
        resValue 'string', 'procPerformanceThresholdSec', properties.getProperty("procPerformanceThresholdSec", "")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def testDataFile = file('../../../../resources/.test/test_data.json')
    def parsedJson = new groovy.json.JsonSlurper().parseText(testDataFile.text)
    def languages = []
    parsedJson.tests.parameters.each { a ->
        languages.add(a.language)
    }

    flavorDimensions "language"
    productFlavors {
        languages.each { language ->
            "$language" {
                applicationIdSuffix ".$language"

            }
        }

        all { flavor ->
            delete fileTree("$projectDir/src/main/assets") {
                exclude '**/.gitkeep'
            }
            task("${flavor.name}CopyParams", type: Copy) {
                if (flavor.name != 'en') {
                    from("$projectDir/../../../../lib/common/")
                    include("leopard_params_${flavor.name}.pv")
                    into("$projectDir/src/main/assets/models")
                } else {
                    from("$projectDir/../../../../lib/common/")
                    include("leopard_params.pv")
                    into("$projectDir/src/main/assets/models")
                }
            }
        }
    }
    sourceSets {
        androidTest {
            java {
                if (properties.getProperty("initPerformanceThresholdSec", "").length() == 0 ||
                    properties.getProperty("procPerformanceThresholdSec", "").length() == 0) {
                    exclude "**/PerformanceTest.java"
                } else {
                    exclude "**/LeopardTest.java"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'ai.picovoice:leopard-android:1.2.1'
    implementation 'ai.picovoice:android-voice-processor:1.0.2'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.microsoft.appcenter:espresso-test-extension:1.4')
}

afterEvaluate {
    android.productFlavors.all {
        flavor ->
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyParams"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyParams"
    }
}
